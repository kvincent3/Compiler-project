// FONCTIONS PR�-D�FINIES EN LANAGAGE D'ASSEMBLAGE

// char *itoa(int i, char *p, int b);
//
// i entier � convertir
// p pointeur du tampon d�j� allou� en m�moire o� copier la cha�ne de caract�res
// b base de num�ration utilis�e (de 2 � 36 inclus car il n'y a que 36 chiffres; par exemple: 2, 4, 8, 10, 16)
//
// Convertit un entier en cha�ne de caract�res cod�e en ASCII
// (cette fonction fait partie de la biblioth�que standard portable C stdlib et est normalement �crite en C).
// Limitation ici: b doit �tre pair.
// Retourne le pointeur sur la cha�ne de caract�re
//
// Ce programme terminera automatiquement la cha�ne de caract�res par NUL;
// le tampon devrait avoir une taille suffisante (par exemple sizeof(int)*8+1 octets pour b=2)
// Si la base = 10 et que l'entier est n�gatif la cha�ne de caract�res est pr�c�d�e d'un signe moins (-);
// pour toute autre base, la valeur i est consid�r�e non sign�e.
// Les 36 chiffres utilisables sont dans l'ordre: 0, 1, 2,..., 9, A, B, C, ... , Z .
// Aucune erreur n'est g�r�e.
     

ITOA_I      equ 4      // offset du param�tre i
ITOA_P      equ 6      // offset du param�tre p
ITOA_B      equ 8      // offset du param�tre b

ASCII_MINUS equ 45     // code ASCII de -
ASCII_PLUS  equ 43     // code ASCII de +
ASCII_SP    equ 32     // code ASCII d'espace SP
ASCII_0     equ 48     // code ASCII de z�ro (les autres chiffres jusqu'� 9 suivent dans l'ordre)
ASCII_A     equ 65     // code ASCII de A (les autres lettres jusqu'� Z suivent dans l'ordre alphab�tique)

// LNK: cr�e environnement du main pour permettre des variables locales 
// mais sans encore les r�server
itoa_   stw bp, -(sp)
        ldw bp, sp

// r�cup�ration des param�tres depuis pile vers registres
        ldw r0, (bp)ITOA_I    // r0 = i    
        ldw r1, (bp)ITOA_B    // r1 = b
        
// g�re le signe: normalement itoa g�re des int c'est � dire des entiers sign�s, 
// mais en fait seulement pour b=10;
// dans ce cas calcule le signe dans r3 et charge r0 avec la valeur absolue de i
        ldq ASCII_SP, r3      // code ASCII de espace (SPace) -> r3
        ldq 10, wr            // 10 -> wr
        cmp r1, wr            // charge les indicateurs de b - 10
        bne NOSIGN-$-2        // si non �gal (donc si b != 10) saute en NOSIGN, sinon calcule signe
        ldq ASCII_PLUS, r3    // charge le code ASCII du signe plus + dans r3
        tst r0                // charge les indicateurs de r0 et donc de i
        bge POSIT-$-2         // saute en POSIT si i >= 0
        neg r0, r0            // change le signe de r0
        ldq ASCII_MINUS, r3   // charge le code ASCII du signe moins - dans r3
POSIT   NOP                   // r3 = code ASCII de signe: SP pour aucun, - ou +


// convertit l'entier i en chiffres et les empile de droite � gauche
NOSIGN  ldw r2, r0            // r2 <- r0
CNVLOOP ldw r0, r2            // r0 <- r2
   
   // effectue "cr�ativement" la division par b suppos� pair (car l'instruction div est h�las sign�e ...)
   // d=2*d' , D = d * q + r  , D = 2*D'+r" , D' = d' * q + r' => r = 2*r'+r"
   // un bug appara�t avec SRL R0, R0 avec R0 = 2 : met CF � 1 !!
        srl r1, r1            // r1 = b/2
        ani r0, r4, #1        // ANd Imm�diate entre r0 et 00...01 vers r4:
                              // bit n�0 de r0 -> r4; r4 = reste" de r0/2
        srl r0, r0            // r0 / 2 -> r0
        div r0, r1, r2        // quotient = r0 / r1 -> r2, reste' = r0 % r1 -> r0
        shl r0, r0            // r0 = 2 * reste'
        add r0, r4, r0        // r0 = reste = 2 * reste' + reste" => r0 = chiffre
        shl r1, r1            // r1 = b

        adq -10, r0           // chiffre - 10 -> r0 
        bge LETTER-$-2        // saute en LETTER si chiffre >= 10
        adq 10+ASCII_0, r0    // ajoute 10 => r0 = chiffre, ajoute code ASCII de 0 
                              // => r0 = code ASCII de chiffre
        bmp STKCHR-$-2        // saute en STKCHR 

LETTER  adq ASCII_A, r0       // r0 = ASCII(A) pour chiffre = 10, ASCII(B) pour 11 ...
                              // ajoute code ASCII de A => r = code ASCII de chiffre
STKCHR  stw r0, -(sp)         // empile code ASCII du chiffre 
                              // (sur un mot complet pour pas d�saligner pile)
        tst r2                // charge les indicateurs en fonction du quotient ds r2)
        bne CNVLOOP-$-2       // boucle si quotient non nul; sinon sort

// les caract�res sont maintenant empil�s : gauche en haut et droit en bas

// recopie les caract�res dans le tampon dans le bon ordre: de gauche � droite
        ldw r1, (bp)ITOA_P    // r1 pointe sur le d�but du tampon d�j� allou� 
        stb r3, (r1)+         // copie le signe dans le tampon
CPYLOOP ldw r0, (sp)+         // d�pile code du chiffre gauche (sur un mot) dans r0
        stb r0, (r1)+         // copie code du chiffre dans un Byte du tampon de gauche � droite
        cmp sp, bp            // compare sp et sa valeur avant empilement des caract�res qui �tait bp
        bne CPYLOOP-$-2       // boucle s'il reste au moins un chiffre sur la pile
        ldq NUL, r0           // charge le code du caract�re NUL dans r0
        stb r0, (r1)+         // sauve code NUL pour terminer la cha�ne de caract�res

// termine
        ldw r0, (bp)ITOA_P    // retourne le pointeur sur la cha�ne de caract�res

    // UNLINK: fermeture de l'environnement de la fonction itoa
        ldw sp, bp            // sp <- bp : abandonne infos locales; sp pointe sur ancinne valeur de bp
        ldw bp, (sp)+         // d�pile ancienne valeur de bp dans bp; sp pointe sur adresse de retour

        rts                   // retourne au programme appelant

//-----------------------------------------------------------------------------------------------------

// void print(char* p)     // imprime le texte point� par param�tre p

// pr�pare l'environnement de la fonction appel�e (prologue) :

print_     LDQ 0, R1      // R1 = taille donn�es locales (ici 0) de fonction appel�e

     // LINK R1        // cr�e et lie l'environnement de fonction appel�e
           STW BP, -(SP)  // empile le contenu du registre BP
           LDW BP, SP     // charge contenu SP ds BP qui pointe sur sa sauvegarde
           SUB SP, R1, SP // r�serve R1 octets sur la pile pour la variable locale z

// charge R0 avec le param�tre p de d�placement 4
           LDW R0, (BP)4  // R0 = M[BP + 4]

// affiche texte point� par R0
           TRP #WRITE_EXC // lance trappe n� WRITE_EXC: affiche texte d'adresse R0

// fin de la fonction (�pilogue) :

    // UNLINK 
           LDW SP, BP    // charge SP avec contenu de BP: abandon infos locales
           LDW BP, (SP)+ // d�pile ancien BP dans BP

           RTS  // retour au programme appelant

